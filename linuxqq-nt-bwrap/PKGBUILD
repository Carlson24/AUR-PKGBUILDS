# Maintainer: Kirikaze Chiyuki <me@chyk.ink>
# Maintainer: sukanka
# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

_pkgname=linuxqq
_base_pkgver=3.2.22_251203
_update_pkgver=${_base_pkgver}
#_md5=08c52833
pkgname=linuxqq-nt-bwrap
pkgver="${_update_pkgver//-/_}"
pkgrel=1
epoch=1
pkgdesc="New Linux QQ based on Electron, with bubblewrap sandbox and some tweaks"
arch=('x86_64')
url='https://im.qq.com/linuxqq/index.shtml'
license=('custom')
depends=('at-spi2-core' 'alsa-lib' 'desktop-file-utils' 'gtk3' 'gtk-update-icon-cache' 'libnotify' 'nss'
         'gnutls' 'bubblewrap' 'xdg-user-dirs' 'flatpak-xdg-utils'
         'libvips' 'openslide' 'autoconf' 'libunwind' 'iproute2')
makedepends=('7zip')
optdepends=('libappindicator-gtk3: 以显示托盘图标'
            'gjs: 提供 GNOME Wayland 下的截图支持'
            'slirp4netns: 固定 MAC 地址时需要'
            'socat: 固定 MAC 地址时需要')
provides=('qq' 'linuxqq')
conflicts=('linuxqq')
options=('!emptydirs')
install="${pkgname}.install"
source=('start_mac_fix.sh' 'config.json'
        "https://dldir1.qq.com/qqfile/qq/QQNT/Linux/QQ_${_base_pkgver}_amd64_01.deb")
sha256sums=('4efadec6df7a406cd62bd4598f47df9081a821b3ba2f40a0be9cf87197db23f4'
            'bb2ec0f104da4da7422d9b0f51c71d0ab38ed2a21764a7a643ab42689e098e4b'
            '3f004bdb246b3194d276c636ea96cb40a7e0b813fea470957d1331e80332bd54')

prepare() {
  local base_ver=${_base_pkgver}
  local cur_ver=${_update_pkgver:-${base_ver}}
  local build_ver=${cur_ver#*-}
  sed -i "s|__BASE_VER__|${base_ver}|g;s|__CURRENT_VER__|${cur_ver}|g;s|__BUILD_VER__|${build_ver}|g" config.json start_mac_fix.sh
}

package() {
  # 解压程序包
  tar -xJ -f "data.tar.xz" -C "${pkgdir}"
  chmod 755 "${pkgdir}/opt/QQ/resources/app"
  mv "${pkgdir}/opt/QQ/qq" "${pkgdir}/opt/QQ/electron"

  # 打包相关处理
  mkdir -p "${pkgdir}/opt/QQ/workarounds"
  cp "${srcdir}/config.json" "${pkgdir}/opt/QQ/workarounds/config.json"
  echo "nameserver 10.0.2.3" > "${pkgdir}/opt/QQ/workarounds/resolv.conf"
  cat << "EOF" | install -Dm755 /dev/stdin "${pkgdir}/opt/QQ/workarounds/xdg-open.sh"
#!/bin/bash
URI_TO_OPEN="$1"

if [[ "${URI_TO_OPEN:0:8}" != "jsbridge" ]]; then
  /flatpak-xdg-open "$URI_TO_OPEN"
fi
EOF


  # 将 LICENSE 移动到正确位置
  mkdir -p "${pkgdir}/usr/share/licenses/${_pkgname}"
  ln -s "/opt/QQ/LICENSE.electron.txt" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
  ln -s "/opt/QQ/LICENSES.chromium.html" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSES.chromium.html"

  # 删除原生库
  rm -f "${pkgdir}"/opt/QQ/resources/app/{libssh2.so.1,libunwind*,sharp-lib/libvips-cpp.so.42}

  # 对 desktop 文件做处理，使其使用正确的图标，启动 start.sh
  cp "${srcdir}/start_mac_fix.sh" "${pkgdir}/opt/QQ/start.sh"
  sed -i "s|/opt/QQ/qq|/opt/QQ/start.sh|" "${pkgdir}/usr/share/applications/qq.desktop"
  sed -i "s|Icon=/usr/share/icons/hicolor/512x512/apps/qq.png|Icon=qq|" "${pkgdir}/usr/share/applications/qq.desktop"

  # 这样就可以直接输入 qq 命令启动了
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "/opt/QQ/start.sh" "${pkgdir}/usr/bin/qq"
}
