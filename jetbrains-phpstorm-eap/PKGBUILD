# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

_buildver=251.23774.209
_pkgver=2025.1
_pkgname=phpstorm

pkgname=phpstorm-eap
pkgver=${_pkgver}+${_buildver}
pkgrel=2
pkgdesc='PhpStorm (EAP)'
arch=('x86_64')
url="https://www.jetbrains.com/${_pkgname}/nextversion/"
license=('custom:jetbrains')
depends=('libdbusmenu-glib' 'jbr-runtime')
options=('!strip')
source=("jetbrains-${pkgname}.desktop"
        "https://download.jetbrains.com/webide/PhpStorm-${_buildver}.tar.gz"
        "https://download.jetbrains.com/webide/PhpStorm-${_buildver}.tar.gz.sha256")
sha256sums=('e6a9a2eb28fbfb6a136db528337f56b827fcd4109c40c5e618fd6286aad3e6f2'
            '3baaf25b438c7584b9d2454895b65a25592e0f092559d896169fc6d31fd84925'
            '6e78c59a0d9203de1f3cf0b49ae9b1d1e639d923975818ac4b6cadd4811e9652')
backup=("opt/jetbrains/${pkgname}/bin/${_pkgname}64.vmoptions"
        "opt/jetbrains/${pkgname}/bin/idea.properties")

prepare() {
  sha256sum -c PhpStorm-${_buildver}.tar.gz.sha256
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${pkgname}"
  cp -a "${srcdir}/PhpStorm-${_buildver}/." "${pkgdir}/opt/jetbrains/${pkgname}"

  install -dm 755 "${pkgdir}/usr/bin"
  ln -s "/opt/jetbrains/${pkgname}/bin/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}-eap"

  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/jbr"
  ln -s "/opt/jetbrains/jbr-runtime" "${pkgdir}/opt/jetbrains/${pkgname}/jbr"

  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/license/." "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/license"
  ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/opt/jetbrains/${pkgname}/license"

  install -dm 755 "${pkgdir}/usr/share/applications"
  install -Dm 644 "jetbrains-${pkgname}.desktop" "${pkgdir}/usr/share/applications"

  install -dm 755 "${pkgdir}/usr/share/pixmaps"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}-eap.png"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.png"
  ln -s "/usr/share/pixmaps/${_pkgname}-eap.png" "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.png"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${_pkgname}-eap.svg"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.svg"
  ln -s "/usr/share/pixmaps/${_pkgname}-eap.svg" "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.svg"
}
