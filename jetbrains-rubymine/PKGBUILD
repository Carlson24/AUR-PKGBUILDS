# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

pkgname=rubymine
_version=2025.3.1
_buildver=253.29346.140
pkgver=${_version}+${_buildver}
pkgrel=1
epoch=1
pkgdesc='Empowering Ruby Developers. Code Smarter with Ruby on Rails IDE'
arch=('x86_64')
url="https://www.jetbrains.com/ruby"
license=('custom:jetbrains')
depends=('libdbusmenu-glib' 'jbr-runtime')
options=('!strip')
source=("jetbrains-${pkgname}.desktop"
        "https://download.jetbrains.com/ruby/RubyMine-${_version}.tar.gz"
        "https://download.jetbrains.com/ruby/RubyMine-${_version}.tar.gz.sha256")
sha256sums=('d7772957d2ea7ff37e952d74fc5b96005662253d982de00a63883aa3a5eac800'
            'e9eb2011ca2d5694c58c011690bf544d026e3a7c1c0c8de9bb37a18fa0cf8118'
            'e54a7507eed53de870361dd952dd0a4f01bcbf06cf48212a39be412439ac67b6')

prepare() {
  sha256sum -c RubyMine-${_version}.tar.gz.sha256
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${pkgname}"
  cp -a "${srcdir}/RubyMine-${_version}/." "${pkgdir}/opt/jetbrains/${pkgname}"

  install -dm 755 "${pkgdir}/usr/bin"
  ln -s "/opt/jetbrains/${pkgname}/bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/jbr"
  ln -s "/opt/jetbrains/jbr-runtime" "${pkgdir}/opt/jetbrains/${pkgname}/jbr"

  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/license/." "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/license"
  ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/opt/jetbrains/${pkgname}/license"

  install -dm 755 "${pkgdir}/usr/share/applications"
  install -Dm 644 "jetbrains-${pkgname}.desktop" "${pkgdir}/usr/share/applications"

  install -dm 755 "${pkgdir}/usr/share/pixmaps"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.png"
  ln -s "/usr/share/pixmaps/${pkgname}.png" "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.png"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.svg"
  ln -s "/usr/share/pixmaps/${pkgname}.svg" "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.svg"

  # Forced use Wayland
  echo "-Dawt.toolkit.name=WLToolkit" >>"${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}64.vmoptions"
}
