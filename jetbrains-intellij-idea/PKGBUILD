# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

pkgname=intellij-idea
_pkgname=idea
pkgver=2025.3.1.1
pkgrel=1
pkgdesc='The IDE for Professional Development, in Java and Kotlin'
arch=('x86_64')
url="https://www.jetbrains.com/idea"
license=('custom:jetbrains')
depends=('libdbusmenu-glib' 'jbr-runtime')
options=('!strip')
source=("jetbrains-${pkgname}.desktop"
        "https://download.jetbrains.com/idea/idea-${pkgver}.tar.gz"
        "https://download.jetbrains.com/idea/idea-${pkgver}.tar.gz.sha256")
sha256sums=('4e9a00665363381a9c17e32f115744af38dc5f987f42502de8e01b120ecc2bfc'
            '3a064b22961f3f39b866b64b628558e0d0f708d423a3f9565d43f0e81196997b'
            '9182c73c9e9847f20d540bd15327922bf7705d2d1dca3a7a447dd432d25c25df')

prepare() {
  sha256sum -c idea-${pkgver}.tar.gz.sha256
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${_pkgname}"
  cp -a "${srcdir}/idea-IU-"*/. "${pkgdir}/opt/jetbrains/${_pkgname}"

  install -dm 755 "${pkgdir}/usr/bin"
  ln -s "/opt/jetbrains/${_pkgname}/bin/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"

  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/jbr"
  ln -s "/opt/jetbrains/jbr-runtime" "${pkgdir}/opt/jetbrains/${_pkgname}/jbr"

  install -dm 755 "${pkgdir}/usr/share/licenses/${_pkgname}"
  cp -r "${pkgdir}/opt/jetbrains/${_pkgname}/license/." "${pkgdir}/usr/share/licenses/${_pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/license"
  ln -s "/usr/share/licenses/${_pkgname}" "${pkgdir}/opt/jetbrains/${_pkgname}/license"

  install -dm 755 "${pkgdir}/usr/share/applications"
  install -Dm 644 "jetbrains-${pkgname}.desktop" "${pkgdir}/usr/share/applications"

  install -dm 755 "${pkgdir}/usr/share/pixmaps"
  cp -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.png"
  ln -s "/usr/share/pixmaps/${_pkgname}.png" "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.png"
  cp -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${_pkgname}.svg"
  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.svg"
  ln -s "/usr/share/pixmaps/${_pkgname}.svg" "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.svg"
}
