# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

pkgname=intellij-idea-community
_pkgname=idea-ce
_version=2025.2.1
_buildver=252.25557.131
pkgver=${_version}+${_buildver}
pkgrel=1
epoch=1
pkgdesc='The IDE for Professional Development, in Java and Kotlin'
arch=('x86_64')
url="https://www.jetbrains.com/idea"
license=('custom:jetbrains')
depends=('libdbusmenu-glib' 'jbr-runtime')
options=('!strip')
source=("jetbrains-${pkgname}.desktop"
  "https://download.jetbrains.com/idea/ideaIC-${_version}.tar.gz"
  "https://download.jetbrains.com/idea/ideaIC-${_version}.tar.gz.sha256")
sha256sums=('f9d03b555ca323727a902b00cc39587deca11b1fd6b199056d4c65a3d2a7bdee'
            'fc76fe8b6693b18d5d7385bb005f415287dbd5897b313287b9ef56dd0df9d5bd'
            '9740cbf713ca5a01b5000226d4213ee5d1ee1a4194c6ad85e98bae97d92f0caf')

prepare() {
  sha256sum -c ideaIC-${_version}.tar.gz.sha256
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${_pkgname}"
  cp -a "${srcdir}/idea-IC-${_buildver}/." "${pkgdir}/opt/jetbrains/${_pkgname}"

  install -dm 755 "${pkgdir}/usr/bin"
  ln -s "/opt/jetbrains/${_pkgname}/bin/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"

  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/jbr"
  ln -s "/opt/jetbrains/jbr-runtime" "${pkgdir}/opt/jetbrains/${_pkgname}/jbr"

  install -dm 755 "${pkgdir}/usr/share/licenses/${_pkgname}"
  cp -r "${pkgdir}/opt/jetbrains/${_pkgname}/license/." "${pkgdir}/usr/share/licenses/${_pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/license"
  ln -s "/usr/share/licenses/${_pkgname}" "${pkgdir}/opt/jetbrains/${_pkgname}/license"

  install -dm 755 "${pkgdir}/usr/share/applications"
  install -Dm 644 "jetbrains-${pkgname}.desktop" "${pkgdir}/usr/share/applications"

  install -dm 755 "${pkgdir}/usr/share/pixmaps"
  cp -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/idea.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/idea.png"
  ln -s "/usr/share/pixmaps/${_pkgname}.png" "${pkgdir}/opt/jetbrains/${_pkgname}/bin/idea.png"
  cp -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/idea.svg" "${pkgdir}/usr/share/pixmaps/${_pkgname}.svg"
  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/idea.svg"
  ln -s "/usr/share/pixmaps/${_pkgname}.svg" "${pkgdir}/opt/jetbrains/${_pkgname}/bin/idea.svg"

  # Forced use Wayland
  echo "-Dawt.toolkit.name=WLToolkit" >>"${pkgdir}/opt/jetbrains/${_pkgname}/bin/idea64.vmoptions"
}
