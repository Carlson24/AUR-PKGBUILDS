# Maintainer: D. Can Celasun <can[at]dcc[dot]im>

pkgname=microsoft-vscode-stable-bin
_pkgname=vscode
pkgver=1.102.1
pkgrel=1
pkgdesc="Visual Studio Code (vscode): Editor for building and debugging modern web and cloud applications (official binary version)"
arch=('x86_64')
url="https://code.visualstudio.com/"
license=('custom: commercial')
provides=('code' 'vscode')
conflicts=('code')
# Upstream has signature verification for extensions and stripping breaks it
# See https://github.com/microsoft/vscode/issues/223455#issuecomment-2610001754
options=(!strip)
install=$pkgname.install
# lsof: needed for terminal splitting, see https://github.com/Microsoft/vscode/issues/62991
# xdg-utils: needed for opening web links with xdg-open
depends=(libxkbfile gnupg gtk3 libsecret nss gcc-libs libnotify libxss glibc lsof shared-mime-info xdg-utils alsa-lib)
optdepends=('glib2: Needed for move to trash functionality'
  'libdbusmenu-glib: Needed for KDE global menu'
  'org.freedesktop.secrets: Needed for settings sync'
  # See https://github.com/MicrosoftDocs/live-share/issues/4650
  'icu69: Needed for live share')
source=(vscode-${pkgver}.desktop.in::https://raw.githubusercontent.com/microsoft/vscode/${pkgver}/resources/linux/code.desktop
  vscode-${pkgver}-url-handler.desktop.in::https://raw.githubusercontent.com/microsoft/vscode/${pkgver}/resources/linux/code-url-handler.desktop
  vscode-${pkgver}-workspace.xml.in::https://raw.githubusercontent.com/microsoft/vscode/${pkgver}/resources/linux/code-workspace.xml
  code_x64_${pkgver}.tar.gz::https://update.code.visualstudio.com/${pkgver}/linux-x64/stable
  ${pkgname}.sh)

sha256sums=('2f1782b30c4e040efff655fd9cf477930c5a0c81ddae27749b0cbb922c1d248e'
            'c361efa7e02fcad759ed80d2fbab67877f33219b981578af6fffaf18aeb12d9b'
            '3af748dd6578a1775e8eb7248ba397b7e11840df2ea6ee234ff76fee3dc306cf'
            'ad402eebd6948cb42c0d3e612bc139c9688c7557cd16ac31f5a03ca29d59623f'
            '70bd4b21a906c0bec8446786de111dd08d3d5b7337970822776c467afd9cfb6e')

_set_meta_info() {
  sed 's/@@NAME_LONG@@/Visual Studio Code/g' "$1" |
    sed 's/@@NAME_SHORT@@/Code/g' |
    sed 's/@@NAME@@/code/g' |
    sed 's#@@EXEC@@#/usr/bin/code#g' |
    sed 's/@@ICON@@/visual-studio-code/g' |
    sed 's/@@URLPROTOCOL@@/vscode/g'
}

prepare() {
  _set_meta_info "${srcdir}/vscode-${pkgver}.desktop.in" >"${srcdir}/code.desktop"
  _set_meta_info "${srcdir}/vscode-${pkgver}-url-handler.desktop.in" >"${srcdir}/code-url-handler.desktop"
  _set_meta_info "${srcdir}/vscode-${pkgver}-workspace.xml.in" >"${srcdir}/code-workspace.xml"
}

package() {

  install -dm 755 "${pkgdir}/opt/microsoft/${_pkgname}"
  cp -r "${srcdir}/VSCode-linux-x64/"* "${pkgdir}/opt/microsoft/${_pkgname}"

  install -dm 755 "${pkgdir}/usr/bin"
  install -Dm 755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/code"

  install -dm 755 "${pkgdir}/usr/share/"{applications,pixmaps,mime/packages,licenses/${_pkgname}}
  install -Dm 644 "${srcdir}/VSCode-linux-x64/resources/app/LICENSE.rtf" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE.rtf"
  install -Dm 644 "${srcdir}/VSCode-linux-x64/resources/app/resources/linux/code.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
  install -Dm 644 "${srcdir}/code.desktop" "${pkgdir}/usr/share/applications/code.desktop"
  install -Dm 644 "${srcdir}/code-url-handler.desktop" "${pkgdir}/usr/share/applications/code-url-handler.desktop"
  install -Dm 644 "${srcdir}/code-workspace.xml" "${pkgdir}/usr/share/mime/packages/code-workspace.xml"
  install -Dm 644 "${srcdir}/VSCode-linux-x64/resources/completions/bash/code" "${pkgdir}/usr/share/bash-completion/completions/code"
  install -Dm 644 "${srcdir}/VSCode-linux-x64/resources/completions/zsh/_code" "${pkgdir}/usr/share/zsh/site-functions/_code"
}
