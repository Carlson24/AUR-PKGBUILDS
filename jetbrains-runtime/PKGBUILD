# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

_buildver=b631.42
_javaver=21.0.6
_pkgname=jbr

pkgname=jbr-runtime
pkgver=${_javaver}+${_buildver}
pkgrel=1
epoch=1
pkgdesc='JetBrains Java Runtime'
arch=('x86_64')
url="https://github.com/JetBrains/JetBrainsRuntime"
license=('custom:jetbrains')
options=('!strip')
source=("https://cache-redirector.jetbrains.com/intellij-jbr/jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz"
        "https://cache-redirector.jetbrains.com/intellij-jbr/jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz.checksum")
sha256sums=('4268591cc34b4ce1754f96fc4c0e34868eced9d9feafc64c2a06557667b738e3'
            '32c04974b9531c9a54813775552187e7f2ad63d235502a62319917b5554c98ed')

prepare() {
  sha512sum -c jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz.checksum
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${pkgname}"
  cp -a "${srcdir}/jbr_jcef-${_javaver}-linux-x64-${_buildver}/." "${pkgdir}/opt/jetbrains/${pkgname}"

  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r "${srcdir}/jbr_jcef-${_javaver}-linux-x64-${_buildver}/legal/." "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/legal"
  ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/opt/jetbrains/${pkgname}/legal"
}
