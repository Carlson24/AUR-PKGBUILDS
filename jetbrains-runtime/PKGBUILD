# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

_buildver=b895.109
_javaver=21.0.6
_pkgname=jbr

pkgname=jbr-runtime
pkgver=${_javaver}+${_buildver}
pkgrel=1
epoch=1
pkgdesc='JetBrains Java Runtime'
arch=('x86_64')
url="https://github.com/JetBrains/JetBrainsRuntime"
license=('custom:jetbrains')
options=('!strip')
source=("https://cache-redirector.jetbrains.com/intellij-jbr/jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz"
        "https://cache-redirector.jetbrains.com/intellij-jbr/jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz.checksum")
sha256sums=('c83568579c23a700d4dbe77aeab762b1f9d522013c2011f41eda08c07ed57913'
            '067e3c07247316b8485bd3b84c3e4ec9b46cbf9bafe3356c5d04f09a3de72701')

prepare() {
  sha512sum -c jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz.checksum
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${pkgname}"
  cp -a "${srcdir}/jbr_jcef-${_javaver}-linux-x64-${_buildver}/." "${pkgdir}/opt/jetbrains/${pkgname}"

  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r "${srcdir}/jbr_jcef-${_javaver}-linux-x64-${_buildver}/legal/." "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/legal"
  ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/opt/jetbrains/${pkgname}/legal"
}
