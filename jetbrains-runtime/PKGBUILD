# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

_buildver=b1038.76
_javaver=21.0.9
_pkgname=jbr

pkgname=jbr-runtime
pkgver=${_javaver}+${_buildver}
pkgrel=1
epoch=1
pkgdesc='JetBrains Java Runtime'
arch=('x86_64')
url="https://github.com/JetBrains/JetBrainsRuntime"
license=('custom:jetbrains')
options=('!strip')
source=("https://cache-redirector.jetbrains.com/intellij-jbr/jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz"
  "https://cache-redirector.jetbrains.com/intellij-jbr/jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz.checksum")
sha256sums=('b442645779d959e632fca152947d9196160f68b524943e947f37d30f42336c44'
            'b26e0995d1750ca8c1b73988a984c5123beff8d11139c022c920b34a9ffe48a5')

prepare() {
  sha512sum -c jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz.checksum
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${pkgname}"
  cp -a "${srcdir}/jbr_jcef-${_javaver}-linux-x64-${_buildver}/." "${pkgdir}/opt/jetbrains/${pkgname}"

  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r "${srcdir}/jbr_jcef-${_javaver}-linux-x64-${_buildver}/legal/." "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/legal"
  ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/opt/jetbrains/${pkgname}/legal"
}
