# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

_buildver=b176.4
_javaver=25
_pkgname=jbr

pkgname=jbr-runtime
pkgver=${_javaver}+${_buildver}
pkgrel=1
epoch=1
pkgdesc='JetBrains Java Runtime'
arch=('x86_64')
url="https://github.com/JetBrains/JetBrainsRuntime"
license=('custom:jetbrains')
options=('!strip')
source=("https://cache-redirector.jetbrains.com/intellij-jbr/jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz"
        "https://cache-redirector.jetbrains.com/intellij-jbr/jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz.checksum")
sha256sums=('81145f2a3d5991d8441b26d4361c2865ac125d5634a23d8c1887bf01b81a26be'
            'c64afdda85482b6ca98de4cb6bdb66b9436d73dadd8880705c6ab4b1c98fee6f')

prepare() {
  sha512sum -c jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz.checksum
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${pkgname}"
  cp -a "${srcdir}/jbr_jcef-${_javaver}-linux-x64-${_buildver}/." "${pkgdir}/opt/jetbrains/${pkgname}"

  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r "${srcdir}/jbr_jcef-${_javaver}-linux-x64-${_buildver}/legal/." "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/legal"
  ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/opt/jetbrains/${pkgname}/legal"
}
