# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

_buildver=b1038.71
_javaver=21.0.8
_pkgname=jbr

pkgname=jbr-runtime
pkgver=${_javaver}+${_buildver}
pkgrel=1
epoch=1
pkgdesc='JetBrains Java Runtime'
arch=('x86_64')
url="https://github.com/JetBrains/JetBrainsRuntime"
license=('custom:jetbrains')
options=('!strip')
source=("https://cache-redirector.jetbrains.com/intellij-jbr/jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz"
  "https://cache-redirector.jetbrains.com/intellij-jbr/jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz.checksum")
sha256sums=('426e5bc38048878e7fd4998d4e6bb44036b7a516263526328e8a3d91e6d95407'
            'ddca9db0089c450989948a05a61caf299c308963d5caeb5454e164760b960488')

prepare() {
  sha512sum -c jbr_jcef-${_javaver}-linux-x64-${_buildver}.tar.gz.checksum
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${pkgname}"
  cp -a "${srcdir}/jbr_jcef-${_javaver}-linux-x64-${_buildver}/." "${pkgdir}/opt/jetbrains/${pkgname}"

  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r "${srcdir}/jbr_jcef-${_javaver}-linux-x64-${_buildver}/legal/." "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/legal"
  ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/opt/jetbrains/${pkgname}/legal"
}
