# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

pkgname=intellij-idea-ultimate
_pkgname=idea
_version=2025.1.4.1
_buildver=251.27812.49
pkgver=${_version}+${_buildver}
pkgrel=1
epoch=1
pkgdesc='The IDE for Professional Development, in Java and Kotlin'
arch=('x86_64')
url="https://www.jetbrains.com/idea"
license=('custom:jetbrains')
depends=('libdbusmenu-glib' 'jbr-runtime')
options=('!strip')
source=("jetbrains-${pkgname}.desktop"
        "https://download.jetbrains.com/idea/ideaIU-${_version}.tar.gz"
        "https://download.jetbrains.com/idea/ideaIU-${_version}.tar.gz.sha256")
sha256sums=('031f4adf5c9db1c734a16fb31c95737ce172d0d6b4e74a4c5a6276ce6bb095e1'
            'd1335bcdcac3746a10d6b3346f06ab6d34c9ad14c4af759322d9a93f7b428894'
            '82dfbe322b8bec8c52fa534cc045db402f43cd6df82f91052180d6e10606c5f7')

prepare() {
  sha256sum -c ideaIU-${_version}.tar.gz.sha256
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${_pkgname}"
  cp -a "${srcdir}/idea-IU-${_buildver}/." "${pkgdir}/opt/jetbrains/${_pkgname}"

  install -dm 755 "${pkgdir}/usr/bin"
  ln -s "/opt/jetbrains/${_pkgname}/bin/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"

  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/jbr"
  ln -s "/opt/jetbrains/jbr-runtime" "${pkgdir}/opt/jetbrains/${_pkgname}/jbr"

  install -dm 755 "${pkgdir}/usr/share/licenses/${_pkgname}"
  cp -r "${pkgdir}/opt/jetbrains/${_pkgname}/license/." "${pkgdir}/usr/share/licenses/${_pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/license"
  ln -s "/usr/share/licenses/${_pkgname}" "${pkgdir}/opt/jetbrains/${_pkgname}/license"

  install -dm 755 "${pkgdir}/usr/share/applications"
  install -Dm 644 "jetbrains-${pkgname}.desktop" "${pkgdir}/usr/share/applications"

  install -dm 755 "${pkgdir}/usr/share/pixmaps"
  cp -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.png"
  ln -s "/usr/share/pixmaps/${_pkgname}.png" "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.png"
  cp -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${_pkgname}.svg"
  rm -r "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.svg"
  ln -s "/usr/share/pixmaps/${_pkgname}.svg" "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}.svg"

  # Forced use Wayland
  echo "-Dawt.toolkit.name=WLToolkit" >> "${pkgdir}/opt/jetbrains/${_pkgname}/bin/${_pkgname}64.vmoptions"
}
