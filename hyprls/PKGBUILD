# Maintainer: LJ <aur.lj at munally.com>
# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

pkgname=hyprls
pkgver=0.12.0
pkgrel=1
pkgdesc="A LSP server for Hyprland config files"
arch=('x86_64')
url="https://github.com/hyprland-community/hyprls"
license=('MIT')
optdepends=('hyprland')
makedepends=('git' 'go' 'just')
source=("git+$url#tag=v$pkgver" "git+https://github.com/hyprwm/hyprland-wiki.git")
b2sums=('40f38df1f788c84ad54bd326b5ef2ba5ebee3b11c917992755b8c93216c009780b588a183df5f880bd376718632182ce037f3d00d87ffe775f9c6d979d2cc8a8'
        'SKIP')

prepare() {
  cd "$srcdir/hyprls"
  git submodule init
  git config submodule.hyprland-wiki.url "$srcdir/hyprland-wiki"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$srcdir/hyprls"
  just build
}

package() {
  install -Dm755 "$srcdir/hyprls/hyprls" "$pkgdir/usr/bin/hyprls"
}
