# Maintainer: LJ <aur.lj at munally.com>
# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

pkgname=hyprls
pkgver=0.11.0
pkgrel=1
pkgdesc="A LSP server for Hyprland config files"
arch=('x86_64')
url="https://github.com/hyprland-community/hyprls"
license=('MIT')
optdepends=('hyprland')
makedepends=('git' 'go' 'just')
source=("git+$url#tag=v$pkgver" "git+https://github.com/hyprwm/hyprland-wiki.git")
b2sums=('a36a9d628bbe9a93ae580ec8dbf922df4393691b165879ff4584eeec0942945ac8932c53c02d246f13436e48b2ef8c58e8ceaf4f033e655c805c98e0a553976e'
        'SKIP')

prepare() {
  cd "$srcdir/hyprls"
  git submodule init
  git config submodule.hyprland-wiki.url "$srcdir/hyprland-wiki"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$srcdir/hyprls"
  just build
}

package() {
  install -Dm755 "$srcdir/hyprls/hyprls" "$pkgdir/usr/bin/hyprls"
}
