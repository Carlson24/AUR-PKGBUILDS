# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

pkgname=datagrip
_version=2025.3.2
_buildver=253.29346.170
pkgver=${_version}+${_buildver}
pkgrel=1
epoch=1
pkgdesc='Your powerful cross-platform IDE for relational and NoSQL databases'
arch=('x86_64')
url="https://www.jetbrains.com/datagrip"
license=('custom:jetbrains')
depends=('libdbusmenu-glib' 'jbr-runtime')
options=('!strip')
source=("jetbrains-${pkgname}.desktop"
        "https://download.jetbrains.com/datagrip/datagrip-${_version}.tar.gz"
        "https://download.jetbrains.com/datagrip/datagrip-${_version}.tar.gz.sha256")
sha256sums=('2587382271da13e5ed3a83b2692f64630dedb998a574da2006e70d5e0804fb61'
            'cb6ed6475d21eb8d30754131bd24003c114096bfa84ffccf334dc11cf8ac60cc'
            'a2ccd4fc9dc8365ce8c60825df6a2695ef521253d79c71983e06db6870a4a5fe')

prepare() {
  sha256sum -c datagrip-${_version}.tar.gz.sha256
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${pkgname}"
  cp -a "${srcdir}/DataGrip-${_version}/." "${pkgdir}/opt/jetbrains/${pkgname}"

  install -dm 755 "${pkgdir}/usr/bin"
  ln -s "/opt/jetbrains/${pkgname}/bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/jbr"
  ln -s "/opt/jetbrains/jbr-runtime" "${pkgdir}/opt/jetbrains/${pkgname}/jbr"

  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/license/." "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/license"
  ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/opt/jetbrains/${pkgname}/license"

  install -dm 755 "${pkgdir}/usr/share/applications"
  install -Dm 644 "jetbrains-${pkgname}.desktop" "${pkgdir}/usr/share/applications"

  install -dm 755 "${pkgdir}/usr/share/pixmaps"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.png"
  ln -s "/usr/share/pixmaps/${pkgname}.png" "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.png"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.svg"
  ln -s "/usr/share/pixmaps/${pkgname}.svg" "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.svg"
}
