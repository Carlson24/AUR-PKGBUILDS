# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

_buildver=251.23774.202
_pkgver=2025.1
_pkgname=clion

pkgname=clion-eap
pkgver=${_pkgver}+${_buildver}
pkgrel=2
pkgdesc='CLion (EAP)'
arch=('x86_64')
url="https://www.jetbrains.com/${_pkgname}/nextversion/"
license=('custom:jetbrains')
depends=('libdbusmenu-glib' 'jbr-runtime')
options=('!strip')
source=("jetbrains-${pkgname}.desktop"
        "https://download.jetbrains.com/cpp/CLion-${_buildver}.tar.gz"
        "https://download.jetbrains.com/cpp/CLion-${_buildver}.tar.gz.sha256")
sha256sums=('29470d7702618a359a903a2ea7652d9d9138c993f4826fea33f0d985f9754b75'
            '0c9665b0f8360fa53d9fcf74d78a35aac74645fe2d38249737cf6aa7ccd50ab4'
            'bb641b549ad084f97767358c7d7f5b8a6e8c367300b9e0145d21ab7551235c2d')
backup=("opt/jetbrains/${pkgname}/bin/${_pkgname}64.vmoptions"
        "opt/jetbrains/${pkgname}/bin/idea.properties")

prepare() {
  sha256sum -c CLion-${_buildver}.tar.gz.sha256
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${pkgname}"
  cp -a "${srcdir}/clion-${_buildver}/." "${pkgdir}/opt/jetbrains/${pkgname}"

  install -dm 755 "${pkgdir}/usr/bin"
  ln -s "/opt/jetbrains/${pkgname}/bin/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}-eap"

  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/jbr"
  ln -s "/opt/jetbrains/jbr-runtime" "${pkgdir}/opt/jetbrains/${pkgname}/jbr"

  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/license/." "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/license"
  ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/opt/jetbrains/${pkgname}/license"

  install -dm 755 "${pkgdir}/usr/share/applications"
  install -Dm 644 "jetbrains-${pkgname}.desktop" "${pkgdir}/usr/share/applications"

  install -dm 755 "${pkgdir}/usr/share/pixmaps"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}-eap.png"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.png"
  ln -s "/usr/share/pixmaps/${_pkgname}-eap.png" "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.png"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${_pkgname}-eap.svg"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.svg"
  ln -s "/usr/share/pixmaps/${_pkgname}-eap.svg" "${pkgdir}/opt/jetbrains/${pkgname}/bin/${_pkgname}.svg"
}
