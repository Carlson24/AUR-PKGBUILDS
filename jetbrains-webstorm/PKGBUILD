# Maintainer: Aubrey Carlson (Carlson) <aubreycarlson24@outlook.com>

pkgname=webstorm
_version=2025.2.5
_buildver=252.28238.10
pkgver=${_version}+${_buildver}
pkgrel=1
epoch=1
pkgdesc='The JavaScript and TypeScript IDE. Make development more productive and enjoyable.'
arch=('x86_64')
url="https://www.jetbrains.com/rust"
license=('custom:jetbrains')
depends=('libdbusmenu-glib' 'jbr-runtime')
options=('!strip')
source=("jetbrains-${pkgname}.desktop"
  "https://download.jetbrains.com/webstorm/WebStorm-${_version}.tar.gz"
  "https://download.jetbrains.com/webstorm/WebStorm-${_version}.tar.gz.sha256")
sha256sums=('251d1d5274d8e350b94ddadce4cfd2f40b8ba7833910cf8da45d7b63c419cfec'
            '535167a94350f355e8d4b63624df25c9eca58e5f570df596c7ca8ab630f3c485'
            'a05d867db6b56c5ad63b3abc2b74bcbb0d6e023ac465e3027b53723d94e60a9d')

prepare() {
  sha256sum -c WebStorm-${_version}.tar.gz.sha256
}

package() {
  install -dm 755 "${pkgdir}/opt/jetbrains/${pkgname}"
  cp -a "${srcdir}/WebStorm-${_buildver}/." "${pkgdir}/opt/jetbrains/${pkgname}"

  install -dm 755 "${pkgdir}/usr/bin"
  ln -s "/opt/jetbrains/${pkgname}/bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/jbr"
  ln -s "/opt/jetbrains/jbr-runtime" "${pkgdir}/opt/jetbrains/${pkgname}/jbr"

  install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/license/." "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/license"
  ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/opt/jetbrains/${pkgname}/license"

  install -dm 755 "${pkgdir}/usr/share/applications"
  install -Dm 644 "jetbrains-${pkgname}.desktop" "${pkgdir}/usr/share/applications"

  install -dm 755 "${pkgdir}/usr/share/pixmaps"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.png"
  ln -s "/usr/share/pixmaps/${pkgname}.png" "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.png"
  cp -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
  rm -r "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.svg"
  ln -s "/usr/share/pixmaps/${pkgname}.svg" "${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}.svg"

  # Forced use Wayland
  echo "-Dawt.toolkit.name=WLToolkit" >>"${pkgdir}/opt/jetbrains/${pkgname}/bin/${pkgname}64.vmoptions"
}
